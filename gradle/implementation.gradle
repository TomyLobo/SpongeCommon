// Shared Gradle configuration for the Sponge implementation projects
buildscript {
    repositories {
        maven {
            name = 'plugins'
            url = 'https://plugins.gradle.org/m2'
        }
    }

    dependencies {
        // TODO: keep this version synced with SpongeAPI somehow
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}
ext.common = project(':SpongeCommon')

// Apply shared ForgeGradle configuration
apply from: common.file('gradle/minecraft.gradle')

ext.apiVersion = (api.version - '-SNAPSHOT') + '-BETA'

dependencies {
    compile common
}

// Eclipse is too stupid to order the classpath correctly unfortunately...
// Put Common last
eclipse {
    classpath {
        file {
            whenMerged { classpath ->
                def lib = classpath.entries.find { it.path == '/SpongeCommon' }
                def others = classpath.entries.findAll { it != lib }
                classpath.entries = others + lib
            }
        }
    }
}

// Nothing comes out of the regular jar task: we shadow both
configurations.runtime.artifacts.removeAll { it.archiveTask.is jar }

ext.ats = ['common_at.cfg']
// The version can change until we're done configuring the project, so delay setting the manifest entries until that is final
afterEvaluate {
    manifest {
        attributes(
                'FMLAT': project.ats.join(' '),
                'Implementation-Version': "$version+$ciSystem-b${buildNumber}.git-$commit"
        )
    }
}

// Finish configuring SpongeCommon first
evaluationDependsOn common.path

// Apply shadow plugin to include the dependencies in the JAR
apply plugin: 'com.github.johnrengelman.shadow'

// Shared dependency config
ext.shadowDependencies = {
    // SpongeCommon and Mixin
    include project(common.path)
    include dependency('org.spongepowered:mixin')

    // SpongeAPI and event generation
    include project(api.path)
    include dependency('org.spongepowered:event-gen-core')

    // Include all other dependencies
    include dependency('org.slf4j:slf4j-api')
    include dependency('org.apache.logging.log4j:log4j-slf4j-impl')

    include dependency('com.google.inject:guice')
    include dependency('javax.inject:javax.inject')
    include dependency('aopalliance:aopalliance')

    include dependency('com.flowpowered:flow-math')
    include dependency('com.flowpowered:flow-noise')

    include dependency('ninja.leaping.configurate:configurate-core')
    include dependency('ninja.leaping.configurate:configurate-hocon')
    include dependency('ninja.leaping.configurate:configurate-gson')
    include dependency('ninja.leaping.configurate:configurate-yaml')

    include dependency('org.yaml:snakeyaml')

    include dependency('com.zaxxer:HikariCP')
    include dependency('org.javassist:javassist')

    include dependency('org.mariadb.jdbc:mariadb-java-client')
    include dependency('com.h2database:h2')
    include dependency('org.xerial:sqlite-jdbc')
}

shadowJar {
    // Make sure SpongeCommon was actually reobfuscated before shading it
    dependsOn common.tasks.reobfJar

    classifier = ''

    // Exclude ForgeGradle classes from shaded JAR
    exclude 'GradleStart**'
    exclude 'net/minecraftforge/**'
    exclude 'dummyThing'

    from jar.refMaps
    from sourceSets.java6.output

    dependencies shadowDependencies

    // We include multiple JDBC drivers, so we need to merge the service file to enable them all
    mergeServiceFiles()

    // Prevent other dependencies replacing our license file
    exclude 'LICENSE', 'NOTICE'
}

afterEvaluate {
    // Reobfuscate the shaded JAR directly
    reobfJar {
        dependsOn shadowJar
        jar = shadowJar.archivePath
    }
}

// Add Common access transformers
minecraft {
    atSource common.sourceSets.main
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar;
task shadowDeobfJar(type: ShadowJar, dependsOn: [classes, api.tasks.classes, common.tasks.classes]) {
    classifier = 'deobf'
    configurations = [project.configurations.runtime]

    // Exclude ForgeGradle classes from shaded JAR
    exclude 'GradleStart**'
    exclude 'net/minecraftforge/**'

    exclude '**refmap.json'
    from api.sourceSets.main.output
    from common.sourceSets.main.output
    from sourceSets.main.output
    from sourceSets.java6.output

    // Some conventional config normally applied for shadowJar {}
    manifest.inheritFrom project.tasks.jar.manifest
    doFirst {
        def files = project.configurations.findByName('shadow').files
        if (files) {
            def libs = [project.tasks.jar.manifest.attributes.get('Class-Path')]
            libs.addAll files.collect { "${it.name}" }
            manifest.attributes 'Class-Path': libs.findAll { it }.join(' ')
        }
    }
    exclude 'META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'

    dependencies shadowDependencies

    // We include multiple JDBC drivers, so we need to merge the service file to enable them all
    mergeServiceFiles()

    // Prevent other dependencies replacing our license file
    exclude 'LICENSE', 'NOTICE'
}

// Forget about having common as a dep, we shade it
uploadArchives.repositories.mavenDeployer.pom*.whenConfigured { pom ->
    pom.dependencies.removeAll { it.artifactId == 'spongecommon' }
}

sourceJar {
    from api.sourceSets.main.allSource
    from common.sourceSets.main.allSource
    from sourceSets.java6.allSource
}

javadocJar {
    dependsOn api.tasks.javadoc
    dependsOn common.tasks.javadoc
    from api.tasks.javadoc.destinationDir
    from common.tasks.javadoc.destinationDir
}

task sdkZip(type: Zip) {
    enabled = false // project.file("sdk").exists()
    classifier = 'sdk'

    from fileTree("sdk");
    rename ".*\\.gitignore", ".gitignore"
    def apiVersion = "";
    if (project.ext.has('forgeVersion'))
        apiVersion = project.minecraft.version + "-" + project.ext.forgeVersion
    eachFile { file ->
        if (file.getName() == "build.gradle")
        {
           file.expand([
                "mcVersion": project.minecraft.version,
                "apiVersion": apiVersion,
                "mappings": project.minecraft.mappings,
                "version": project.version
           ])
        }
    }
}

artifacts {
    archives shadowJar
    archives shadowDeobfJar
    // Include sdk ZIP only if it's actually supported for the implementation
    if (sdkZip.enabled) {
        archives sdkZip
    }
}
